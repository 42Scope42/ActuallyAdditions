buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.8.9-r22_beta2"
group = "de.ellpeck.actuallyadditions"
archivesBaseName = "ActuallyAdditions"

minecraft {
    version = "1.8.9-11.15.0.1684"
    runDir = "idea"

    mappings = "stable_20"
    makeObfSourceJar = false

    replaceIn "ModUtil.java"
    replace "@VERSION@", project.version.toString()
}

repositories {
    maven {
        url "http://chickenbones.net/maven/"
    }
}

dependencies {
    compile "codechicken:CodeChickenLib:1.8-1.1.2.139:dev"
    compile "codechicken:CodeChickenCore:1.8-1.0.5.36:dev"
    compile "codechicken:NotEnoughItems:1.8-1.0.5.104:dev"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'de/ellpeck/actuallyadditions/api/**'
}